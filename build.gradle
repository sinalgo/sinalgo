/*
BSD 3-Clause License

Copyright (c) 2007-2013, Distributed Computing Group (DCG)
                         ETH Zurich
                         Switzerland
                         dcg.ethz.ch
              2017-2018, Andr√© Brait

All rights reserved.

* Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:

* Redistributions of source code must retain the above copyright notice, this
list of conditions and the following disclaimer.

* Redistributions in binary form must reproduce the above copyright notice,
this list of conditions and the following disclaimer in the documentation
and/or other materials provided with the distribution.

* Neither the name of the copyright holder nor the names of its
contributors may be used to endorse or promote products derived from
this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.2'
    id 'io.freefair.lombok' version '3.6.6'
}

lombok {
    version = "1.18.8"
}

version = rootProject.file('VERSION').text.trim()

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'distribution'

apply plugin: 'idea'
apply plugin: 'eclipse'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileJava {
    options.compilerArgs = ['--release', '8']
}

distTar.enabled = false
distZip.enabled = false
startScripts.enabled = false
jar.enabled = false
shadowDistTar.enabled = false

mainClassName = "sinalgo.Run"

javadoc {
    title = "Sinalgo API Documentation Version: ${version}"
    failOnError = false
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.jdom', name: 'jdom2', version: '2.0.6'
    compile group: 'com.apple', name: 'AppleJavaExtensions', version: '1.4'
    compile group: 'io.github.classgraph', name: 'classgraph', version: '4.8.39'
    //testCompile group: 'junit', name: 'junit', version: '4.12' // TODO enable tests
}

processResources {
    with copySpec {
        from 'VERSION'
        from 'WEB_PAGE_URL'
        from 'WEB_REPOSITORY_URL'
    }

    with copySpec {
        from 'icon/sinalgo_48.png'
        rename { 'sinalgo.png' }
        into 'sinalgo/images'
    }

    with copySpec {
        from 'icon/sinalgo_21.png'
        into 'sinalgo/images'
    }
}

run {
    if (project.hasProperty("appArgs")) {
        appArgs = project.getProperties().get("appArgs")
        if (appArgs instanceof String) {
            args = Eval.me(appArgs)
        }
    }
}

delombok {
    generateLombokConfig.enabled = false
}